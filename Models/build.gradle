/*
 * Copyright 2021 JaaJSoft
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java-library'
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id "maven-publish"
    id 'jacoco'
}

group 'dev.jaaj.fx.terminal'
version '0.1'

javafx {
    version = '15'
    modules = ['javafx.base', 'javafx.graphics']
}

sourceCompatibility = 15

compileJava.options.encoding = 'UTF-8'

repositories {
    maven {
        url 'https://nexus.jaaj.dev/repository/maven-public'
    }
    mavenCentral()
    jcenter()

}
dependencies {
    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.28'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'dev.jaaj.fx:core:0.3.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version
            from components.java
            pom {
                name = 'models'
                description = 'A JavaFX terminal app'
                url = 'https://github.com/JaaJSoft/JaaJFXTerminal'
                /*properties = [
                        myProp: "value",
                        "prop.with.dots": "anotherValue"
                ]*/
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    url = 'https://github.com/JaaJSoft/JaaJFX-core'
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username System.getenv('REPO_USER')
                password System.getenv('REPO_PASSWORD')
            }
            def releasesRepoUrl = "https://nexus.jaaj.dev/repository/maven-releases/"
            def snapshotsRepoUrl = "https://nexus.jaaj.dev/repository/maven-snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

test {
    finalizedBy jacocoTestReport // report is always generated after tests run
}
jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}
