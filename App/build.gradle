/*
 * Copyright 2021 JaaJSoft
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id 'org.beryx.runtime' version '1.12.1'
    id "com.github.johnrengelman.shadow" version "6.1.0"
}

javafx {
    version = '15'
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.web']
}

group 'dev.jaaj.fx.terminal'
version '0.1'

sourceCompatibility = 15

mainClassName = 'dev.jaaj.fx.terminal.app.Main'

compileJava.options.encoding = 'UTF-8'

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        noConsole = true
    }
    jpackage {
        def currentOs = org.gradle.internal.os.OperatingSystem.current()
        def imgType = currentOs.windows ? 'ico' : currentOs.macOsX ? 'icns' : 'png'
        imageOptions += ['--icon', "src/main/resources/dev/jaaj/fx/terminal/app/img/icon.$imgType"]
        installerOptions += ['--resource-dir', "src/main/resources"]
        installerOptions += ['--vendor', 'JaaJSoft']
        jvmArgs += "-Dfile.encoding=UTF-8"
        if (currentOs.windows) {
            installerOptions += ['--win-dir-chooser', '--win-menu', '--win-shortcut']
        } else if (currentOs.linux) {
            installerOptions += ['--linux-package-name', rootProject.name, '--linux-shortcut']
        } else if (currentOs.macOsX) {
            installerOptions += ['--mac-package-name', rootProject.name]
        }
        imageName = rootProject.name
        installerName = rootProject.name
    }
}

repositories {
    maven {
        url 'https://nexus.jaaj.dev/repository/maven-public'
    }
    maven {
        url 'https://github.com/javaterminal/terminalfx/raw/master/releases'
    }
    jcenter()
    mavenCentral()
}

dependencies {
    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.28'
    implementation 'dev.jaaj.fx:core:0.3.0'
    implementation 'org.jfxtras:jmetro:11.6.11'
    implementation 'org.controlsfx:controlsfx:11.0.2'
    implementation project(":Controls")
    implementation project(":Models")
    implementation 'dev.dirs:directories:23'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}
